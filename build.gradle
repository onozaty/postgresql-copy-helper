plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version "1.8.4"
}

group = 'com.github.onozaty'
version = '0.0.2'
sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation 'org.postgresql:postgresql:42.2.9'
    implementation 'org.apache.commons:commons-csv:1.7'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'commons-dbutils:commons-dbutils:1.7'

    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'postgresql-copy-helper'
                description = 'Provides a helper class for using the PostgreSQL COPY command.'
                url = 'https://github.com/onozaty/postgresql-copy-helper/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/onozaty/postgresql-copy-helper.git'
                    url = 'https://github.com/onozaty/postgresql-copy-helper/'
                }
                developers {
                    developer {
                        id = "onozaty"
                        name = "Hirokazu Onozato"
                        email = "onozaty@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "file://$buildDir/maven"
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray_api_key') ? bintray_api_key : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = group + ':' + project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/onozaty/postgresql-copy-helper.git'
    }
}